*	XORSHIFT PRNG DEMO PROGRAM
*	FOR THE UNEXPANDED TI-99/4A
*	MICHAEL MARTIN, 2024

	IDT	'XORSHIFT'

*	CARTRIDGE HEADER
	AORG	>6000

	DATA	>AA01,0,0,MENU
	DATA	0,0,0,0
MENU	DATA	0,MAIN
	BYTE	13
	TEXT	'XORSHIFT DEMO'

*	MAIN PROGRAM

	EVEN
MAIN	LWPI	>8300		* SET WORKSPACE
	LI	R9,1		* SEED RNG
	LI	R10,1
	BL	@CLS
	BL	@STDCHR
	LI	R8,>40C2	* LEAVE ROOM FOR HEADER
	LI	R4,64		* 64 ITERATIONS
LP	BL	@RNG
	BL	@PHEX
	DEC	R4
	JNE	LP
	LI	R8,>4044	* PRINT HEADER
	LI	R0,HDR
	BL	@PTXT
	LI	R8,>4000	* FRAME COUNTER
	LI	R0,FRAME
	BL	@PTXT
INF	MOV	R8,R7
	MOV	@>8378,R0
	ANDI	R0,255
	BL	@PHEX
	MOV	R7,R8
	JMP	INF		* LOOP FOREVER ONCE DONE

CLS	LIMI	0
	LI	R0,>2000
	LI	R1,>0040
	MOVB	R1,@>8C02
	SWPB	R1
	MOVB	R1,@>8C02
	LI	R1,768
CLP	MOVB	R0,@>8C00
	DEC	R1
	JNE	CLP
	LIMI	2
	B	*R11

RNG	MOV	R9,R0
	SLA	R0,5
	XOR	R9,R0
	MOV	R0,R9
	SRL	R0,3
	XOR	R9,R0
	XOR	R10,R0
	MOV	R10,R9
	SRL	R10,1
	XOR	R0,R10
	MOV	R10,R0
	B	*R11

PHEX	LIMI	0
	MOV	R0,R1
	MOV	R11,R2
	SWPB	R8
	MOVB	R8,@>8C02
	SWPB	R8
	MOVB	R8,@>8C02
	SRL	R0,4
	BL	@PDIGI
	MOV	R1,R0
	BL	@PDIGI
	SWPB	R1
	MOV	R1,R0
	SRL	R0,4
	BL	@PDIGI
	MOV	R1,R0
	BL	@PDIGI
	MOV	R1,R0
	LI	R1,>2000
	MOV	R1,@>8C00
	AI	R8,8
	MOV	R1,@>8C00
	NOP
	MOV	R1,@>8C00
	NOP
	MOV	R1,@>8C00
	LIMI	2
	B	*R2
PDIGI	ANDI	R0,>0F00
	AI	R0,>3000
	CI	R0,>3A00
	JL	POK
	AI	R0,>0700
POK	MOVB	R0,@>8C00
	B	*R11

PTXT	LIMI	0
	SWPB	R8
	MOVB	R8,@>8C02
	SWPB	R8
	MOVB	R8,@>8C02
PTLP	MOVB	*R0+,R1
	JEQ	PTDONE
	MOVB	R1,@>8C00
	INC	R8
	JMP	PTLP
PTDONE	LIMI	2
	B	*R11

*	COPY STANDARD CHARSET OUT OF GROM
*	(STARTS AT >6B4, WRITTEN TO >900, 7 BYTES AT A TIME)
STDCHR	LIMI	0
	LI	R1,>06B4
	MOVB	R1,@>9C02
	SWPB	R1
	MOVB	R1,@>9C02
	LI	R1,>0049
	MOVB	R1,@>8C02
	SWPB	R1
	MOVB	R1,@>8C02
	LI	R1,32
SCLP	CLR	R0
	MOVB	R0,@>8C00
	LI	R0,7
SCLP2	MOVB	@>9800,@>8C00
	DEC	R0
	JNE	SCLP2
	INC	R1
	CI	R1,127
	JNE	SCLP
	LI	R1,>6949	* TURN DASH INTO FULL UNDERLINE
	MOVB	R1,@>8C02
	SWPB	R1
	MOVB	R1,@>8C02
	LI	R0,>FF00
	MOVB	R0,@>8C00
	SWPB	R0
	LI	R1,6
SCLP3	MOVB	R0,@>8C00
	DEC	R1
	JNE	SCLP3
	LIMI	2
	B	*R11

HDR	TEXT	'XORSHIFT16 TEST SEQUENCE        '
	TEXT	'------------------------'
	BYTE	0
FRAME	TEXT	'FRAME COUNT: '
	BYTE	0
	EVEN

	END MAIN

