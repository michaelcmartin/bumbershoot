	GROM >6000
	AORG >0000

	DATA >AA01,0,0,MENU
	DATA 0,0,0,0

MENU	DATA 0,START
	STRI "GROM XORSHIFT TEST"

START	ALL >20				* CLEAR SCREEN
	BACK >F4
	DST >0900,@>834A
	CALL >0018			* SET UPPERCASE CHARSET
	DST >0B00,@>834A
	CALL >004A			* SET LOWERCASE CHARSET
	ST >F4,V@>0380			* SET COLORS
	MOVE 15,V@>0380,V@>0381
	MOVE 7,V@>0968,V@>0969		* CHANGE DASH TO UNDERLINE
	ST >FF,V@>0969

	FMT
	ROW 0
	COL 0
	HTEX 'FRAME COUNT:'
	ROW 2
	COL 4
	HTEX 'XORSHIFT16 TEST SEQUENCE        '
	HTEX '------------------------'
	ROW 6
	COL 2
	FEND

	DST 1,@>8300
	CALL RNGSEED
	ST 64,@>8303
!	CALL RNG
	DST @>831E,@>8300
	CALL PHEX
	FMT
	HTEX '    '
	FEND
	DEC @>8303
	BR G@-!

!LOOP	FMT
	ROW 0
	COL 13
	FEND
	CLR @>8300
	ST @>8379,@>8301
	CALL G@PHEX
	SCAN
	BR G@-!LOOP
	EXIT

* RNGSEED: Seed RNG with 16-value in @>8300. Must not be zero.
RNGSEED	DST @>8300,@>831C
	DST @>8300,@>831E
	RTN

* RNG: Advance RNG value. 16-bit RNG value is in @>831E.
RNG	DST @>831C,@>8300
	DSLL 5,@>8300
	DXOR @>831C,@>8300
	DST @>8300,@>831C
	DSRL 3,@>8300
	DXOR @>831C,@>8300
	DXOR @>831E,@>8300
	DST @>831E,@>831C
	DSRL 1,@>831E
	DXOR @>8300,@>831E
	RTN

* PHEX: PRINT THE 16-BIT VALUE IN @>8300 TO THE CURRENT FMT POSITION.
*       USES @>8302 AS SCRATCH SPACE.
PHEX	ST @>8300,@>8302
	SRL 4,@>8302
	CALL G@!PDIGI
	ST @>8300,@>8302
	CALL G@!PDIGI
	ST @>8301,@>8302
	SRL 4,@>8302
	CALL G@!PDIGI
	ST @>8301,@>8302		* FALL THROUGH...
!PDIGI	AND >0F,@>8302
	ADD >30,@>8302
	CHE >3A,@>8302
	BR G@!PDOK
	ADD >07,@>8302
!PDOK	FMT
	HSTR 1,@>8302
	FEND
	RTN
