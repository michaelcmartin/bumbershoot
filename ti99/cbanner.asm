* CARTRIDGE HEADER
	AORG	>6000
	DATA	>AA01,0,0,MENU
	DATA	0,0,0,0
MENU	DATA	0,START
	STRI	'BANNER'
	EVEN

	.DEFM	VBLIT
	LI	R0, ((#1 >> 8) & >00FF) | ((#1 << 8) & >FF00) | >0040
	LI	R1, #2
	LI	R2, #3
	BL	@VBLIT
	.ENDM

	.DEFM	VREG
	LI	R0, (#1 & >000F) | ((#2 << 8) & >FF00) | >0080
	MOVB	R0,@>8C02
	SWPB	R0
	MOVB	R0,@>8C02
	.ENDM

* MAIN PROGRAM
START	LWPI	>8300			* SET WORKSPACE REGION
	LIMI	0			* DISABLE INTERRUPTS
	BL	@CLS			* CLEAR SCREEN
	BL	@CHRSET			* CONFIGURE DEFAULT FONT
	.VREG	7,>12
	.VBLIT	>0140,MSG,MSGLEN
	.VBLIT	>0380,COLS,16
	.VBLIT	>0AF0,GFX1,8
	.VBLIT	>0B00,GFX2,32
	.VBLIT	>0B40,GFX3,32
	LIMI	2			* RE-ENABLE INTERRUPTS
!	JMP	-!			* AND LOOP FOREVER

CLS	LI	R0,>2000		* WRITE SPACES
	LI	R1,>0040		* START VRAM POINTER AT 0
	MOVB	R1,@>8C02		* SET VRAM WRITE POINTER
	SWPB	R1
	MOVB	R1,@>8C02
	LI	R1,768			* 768 CHARS ON SCREEN
!LP	MOVB	R0,@>8C00		* WRITE BYTE TO VRAM
	DEC	R1			* DECREMENT COUNTER
	JNE	-!LP			* AND LOOP BACK
	B	*R11			* AND RETURN FROM FUNCTION

CHRSET	LI	R1,>06B4		* SET GROM READ POINTER
	MOVB	R1,@>9C02
	SWPB	R1
	MOVB	R1,@>9C02
	LI	R1,>0049		* SET VRAM WRITE POINTER
	MOVB	R1,@>8C02
	SWPB	R1
	MOVB	R1,@>8C02
	LI	R1,32			* START AT CHARACTER 32
!LP	CLR	R0			* FIRST LINE OF EACH CHAR IS 0
	MOVB	R0,@>8C00
	LI	R0,7			* COPY NEXT 7 LINES FROM GROM
!LP2	MOVB	@>9800,@>8C00
	DEC	R0
	JNE	-!LP2
	INC	R1			* NEXT CHARACTER
	CI	R1,127			* LOOP UNTIL WE'VE FINISHED ALL
	JNE	-!LP			* PRINTABLE ASCII CHARS
	B	*R11			* AND RETURN FROM FUNCTION

VBLIT	MOVB	R0,@>8C02
	SWPB	R0
	MOVB	R0,@>8C02
!LP	MOVB	*R1+,R0
	MOVB	R0,@>8C00
	DEC	R2
	JNE	-!LP
	B	*R11

MSG	TEXT	'  pppppppppppppppppppppppppppp  '
	TEXT	'  p`bppBUMBERSHOOTpSOFTWAREppp  '
	TEXT	'  pak16pMOREpTHANpYOURpTI-83^p  '
	TEXT	'  pppppppppppppppppppppppppppp  '
MSGLEN	EQU	$-MSG

COLS	BYTE >10,>10,>10,>10,>10,>F1,>F1,>F1
	BYTE >F1,>F1,>F1,>F1,>41,>A1,>11,>10

GFX1	BYTE >00,>10,>10,>10,>10,>10,>00,>10
GFX2	BYTE >03,>0F,>1F,>3F,>7F,>7F,>FF,>FF
	BYTE >FF,>FE,>7C,>78,>30,>00,>00,>00
	BYTE >C0,>F0,>F8,>F8,>F0,>E0,>C0,>80
	BYTE >00,>00,>00,>00,>00,>00,>00,>00
GFX3	BYTE >00,>00,>00,>00,>00,>00,>00,>00
	BYTE >00,>00,>00,>00,>00,>00,>00,>00
	BYTE >00,>00,>00,>00,>00,>00,>00,>00
	BYTE >80,>C0,>60,>30,>18,>08,>38,>00

	END START
